plugins {
    id 'java'
    id 'io.quarkus'
    id 'groovy'
    id "org.openapi.generator" version "6.0.0"
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-resteasy-reactive'
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
    implementation("io.quarkus:quarkus-config-yaml")

    //jackson
    implementation("io.quarkus:quarkus-resteasy-reactive-jackson")

    // Hibernate ORM specific dependencies
    implementation("io.quarkus:quarkus-hibernate-orm-panache")

    //hibernate-validator - verifica os campos nulos do request
    implementation("io.quarkus:quarkus-hibernate-validator")


    // JDBC driver dependencies
    implementation("io.quarkus:quarkus-jdbc-mysql")

    //Spock Framework
    implementation 'org.codehaus.groovy:groovy-all:3.0.11'
    implementation 'net.bytebuddy:byte-buddy:1.12.10'
    implementation 'org.spockframework:spock-core:2.1-groovy-3.0'

    //dependencias openApiGenerator
    implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.1'
    implementation 'io.swagger:swagger-annotations:1.6.6'





}

group 'com.somnus'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

task generateModels(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
    generatorName = "jaxrs-spec"
    inputSpec = "$projectDir/src/main/resources/openApi.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "com.crudquarkus.api"
    modelPackage = "com.crudquarkus.model"

    globalProperties = [
            apis: "",
            models: ""
    ]

    configOptions = [
            basePackage: "com.crudquarkus",
            dateLibrary: "legacy",
            interfaceOnly: "true",
            useTags: "true",
            hideGenerationTimeStamp: "true",
            library: "quarkus",
            sourceFolder: "src/main/java"
    ]
}

sourceSets {
    main{
        java{
            srcDir(files("${generateModels.outputDir.get()}/src/main/java"))
        }
    }
}

compileJava.dependsOn tasks.generateModels
